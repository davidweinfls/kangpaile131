
import java_cup.runtime.*;
import java.util.Vector;

//--------------------------------------------------------------------
//
//--------------------------------------------------------------------

parser code 
{:
:};

scan with {: return new Symbol (sym.EOF); :};

//--------------------------------------------------------------------
//
//--------------------------------------------------------------------

terminal
    T_AMPERSAND,
    T_AND,
    T_ARROW,
    T_ASSIGN,
    T_BAR,
    T_BOOL,
    T_BREAK,
    T_CARET,
    T_CHAR,
    T_CIN,
    T_COLON,
    T_COLONCOLON,
    T_COMMA,
    T_CONST,
    T_CONTINUE,
    T_COUT,
    T_DOT,
    T_ELSE,
    T_ENDL,
    T_EQU,
    T_EXIT,
    T_EXTERN,
    T_FALSE,
    T_FLOAT,
    T_FUNCPTR,
    T_FUNCTION,
    T_GT,
    T_GTE,
    T_IF,
    T_INT,
    T_ISTREAM,
    T_LBRACE,
    T_LBRACKET,
    T_LPAREN,
    T_LT,
    T_LTE,
    T_MINUS,
    T_MINUSMINUS,
    T_MOD,
    T_NEQ,
    T_NOT,
    T_NULLPTR,
    T_OR,
    T_OSTREAM,
    T_PLUS,
    T_PLUSPLUS,
    T_RBRACE,
    T_RBRACKET,
    T_RETURN,
    T_RPAREN,
    T_SEMI,
    T_SIZEOF,
    T_SLASH,
    T_STAR,
    T_STATIC,
    T_STRUCTDEF,
    T_TRUE,
    T_TYPEDEF,
    T_WHILE,
    T_VOID,
    T_AUTO,
	T_NEW,
    T_DELETE,
    T_DECLTYPE
    ;

terminal String
    T_CHAR_LITERAL,
    T_FLOAT_LITERAL,
    T_INT_LITERAL,
    T_STR_LITERAL,
    T_ID_U,
    T_ID
    ;


//--------------------------------------------------------------------
//
//--------------------------------------------------------------------

non terminal    
    Program,
    OptGlobalDecls,
    GlobalDecls,
    GlobalDecl,
    FuncDef,
    FuncDecl,
    ExternDecl,
    VarDecl,
    ConstDecl,
    OptStatic,
    TypedefDecl,
    OptRef,
    OptStmtList,
    StmtList,
    Stmt,
    CodeBlock,
    IfStmt,
    OptElse,
    WhileStmt,
    BreakStmt,
    ContinueStmt,
    ExitStmt,
    ReturnStmt,
    ReadStmt,
    WriteStmt,
    WritePairList,
    WritePair,
    ConstExpr,
    OptExprList,
    ExprList,
    UnarySign,
    OptInit,
    ArrElemsList,
    OptModifiers,
	NewStmt,
	DeleteStmt
    ;

non terminal String    
    AddOp,
    MulOp,
    IncDecOp,
    Relation,
    Equality
    ;

non terminal STO
    Expr,
    Expr0,
    Expr1,
    Expr2,
    Expr3,
    Expr4,
    Expr5,
    Expr6,
    Expr7,
    Expr8,
    Designator,
    Designator2,
    Designator3,
    QualIdent,
    ParamDecl
    ;

non terminal Type
    BasicType,
    SubType,
    Type,
    UndecoratedType,
    ReturnType,
    DeclType,
  	OptPointerList,
  	PointerList,
  	Pointer
    ;
    	
non terminal Vector
    IdList,
    IdListUpper,
    IdListWOptInit,
    IdListWInit,
    OptParamList,
    ParamList,
    FieldList,
	FieldsList
    ;

//--------------------------------------------------------------------
//
//--------------------------------------------------------------------

Program ::=    {:
                         ((MyParser) parser).DoProgramStart(); 
                    :}
                    OptGlobalDecls
                    {:
                         ((MyParser) parser).DoProgramEnd();
                    :}
    ;

OptGlobalDecls ::=  GlobalDecls
    |                     /* empty */
    ;

GlobalDecls ::= GlobalDecl
    |               GlobalDecls GlobalDecl
    ;


GlobalDecl ::=  ExternDecl
    |               VarDecl
    |               ConstDecl
    |               TypedefDecl
    |               FuncDecl
    |               FuncDef
    ;

ExternDecl ::= T_EXTERN UndecoratedType IdList:_3 T_SEMI
                    {:
                         ((MyParser) parser).DoExternDecl (_3);
                    :}
    ;

VarDecl ::=     OptStatic UndecoratedType:_2 IdListWOptInit:_3 T_SEMI
                    {:
                         ((MyParser) parser).DoVarDecl (_3, _2);
                    :}
    |           OptStatic T_AUTO T_ID:_4 T_ASSIGN Expr:_5 T_SEMI
    			{:
    				((MyParser) parser).DoInitCheck (_4, _5);
    				Vector<VariableBox<STO,STO>> v = new Vector();
                    VariableBox<STO, STO> box = new VariableBox<STO, STO>
                    (new VarSTO(_4, null), _5);
                    v.addElement(box);
                    ((MyParser) parser).DoVarDecl(v, _5.getType());
    			:}

    ;

ConstDecl ::=  OptStatic T_CONST UndecoratedType:_2 IdListWInit:_3 T_SEMI
               {:
                        ((MyParser) parser).DoConstDecl (_3, _2);
               :}
    |          OptStatic T_CONST T_AUTO T_ID:_4 T_ASSIGN Expr:_5 T_SEMI
    			{:
    				STO s = ((MyParser) parser).DoInitCheck (_4, _5);	//TODO: make sure this is needed
    				STO sto = ((MyParser) parser).DoConstInit (_4, _5);
    				Vector<STO> v = new Vector();
    				v.addElement(sto);
                    ((MyParser) parser).DoConstDecl(v, _5.getType());
    			:}
    ;


OptStatic ::=   T_STATIC
				{: ((MyParser) parser).setStatic ();  :}
    |               /* empty */ {: ((MyParser) parser).resetStatic ();  :}
    ;


TypedefDecl ::=     T_TYPEDEF Type:_1 IdListUpper:_2 T_SEMI
                    {:
                        ((MyParser) parser).DoTypedefDecl (_2, _1);
                    :}
    |               T_STRUCTDEF T_ID_U:_1 T_LBRACE
                    {:
                    	((MyParser) parser).setStruct(_1);
                        ((MyParser) parser).DoBlockOpen();
                    :}
                    FieldsList:_2 T_RBRACE T_SEMI
                    {:
                         ((MyParser) parser).DoBlockClose();
                         ((MyParser) parser).DoStructdefDecl (_1, _2);
                         ((MyParser) parser).resetStruct();
                    :}
    ;


FieldsList ::=	FieldList:_1
				{: 
					RESULT = _1; 
				:}
    |           FieldsList:_1 FieldList:_2
    			{:
    				RESULT = _1;
                    for(int i = 0; i < _2.size(); i++)
                    {
                    	RESULT.addElement(_2.elementAt(i));
                    }
                :}
    ;


FieldList ::= 	Type:_1 IdList:_2 T_SEMI
				{:
					RESULT = ((MyParser) parser).DoFieldListCheck(_1, _2);
				:}
    ;


FuncDef ::=    T_FUNCTION T_COLON ReturnType:_1 OptRef:_4 T_ID:_2
                    {:
                        ((MyParser) parser).SaveLineNum ();
                        ((MyParser) parser).DoFuncDecl_1(_1,  _2, ((Boolean)_4));
                    :}
                    T_LPAREN OptParamList:_3 T_RPAREN
                    {:
                        ((MyParser) parser).DoFormalParams(_3);
                    :}
                    T_LBRACE OptStmtList T_RBRACE
                    {:
                        ((MyParser) parser).DoFuncDecl_2(false);
                    :}
    ;


FuncDecl ::=   T_EXTERN T_FUNCTION T_COLON ReturnType:_4 T_ID:_2
                    {:
                        ((MyParser) parser).DoFuncDecl_1(_4, _2, false);
                    :}
                    T_LPAREN OptParamList:_3 T_RPAREN
                    {:
                        ((MyParser) parser).DoFormalParams(_3);
                    :}
                    T_SEMI
                    {:
                        ((MyParser) parser).DoFuncDecl_2(true);
                    :}
    ;


Type ::=         SubType:_1 OptModifiers:_2
				 {: 
				 	if(_2 != null && !(_2 instanceof ErrorSTO))
                    {
                    	Type typ = ((STO)_2).getType();
                    	if(typ instanceof ArrayType)
                    	{
                            RESULT = new ArrayType ("array", 1,
                                    ((ArrayType) typ).getArraySize(), _1);
                        }
                        else if(typ instanceof PointerType)
                        {
                            ((PointerType) typ).setType (_1);
                           	RESULT = typ;
                        }
                    }
                    else if(_2 instanceof ErrorSTO) 
                    	RESULT = null;
                    else
                        RESULT = _1;
				 :}
    |               T_FUNCPTR T_COLON ReturnType:_1 OptRef:_2 T_LPAREN OptParamList:_3 T_RPAREN
    				{:
    					RESULT = ((MyParser) parser).DoFuncptr(_1, (Boolean)_2, _3);
    				:}
    ;

UndecoratedType ::= SubType:_1 {: RESULT = _1; :}
    |               T_FUNCPTR T_COLON ReturnType:_1 OptRef:_2 T_LPAREN OptParamList:_3 T_RPAREN
    				{:
    					RESULT = ((MyParser) parser).DoFuncptr(_1, (Boolean)_2, _3);
    				:}
    ;

SubType ::=    		QualIdent:_1 {: RESULT = _1.getType(); :}
    |               BasicType:_1 {: RESULT = _1; :}
    |				DeclType:_1 {: RESULT = _1; :}
    ;

DeclType ::= 		T_DECLTYPE T_LPAREN  Expr:_1 {: RESULT = _1.getType(); :} T_RPAREN
	;

BasicType ::=       T_INT:_1 
					{: 
						RESULT = new IntType("int", 4); 
					:}
	
    |               T_FLOAT:_1
    				{:
    					RESULT = new FloatType("float", 4);
    				:}
    |               T_BOOL:_1
    				{:
    					RESULT = new BoolType("bool", 4);
    				:}
    |               T_CHAR
    ;


OptModifiers ::= 	PointerList:_1
					{:
						RESULT = new ExprSTO("PointerList", (PointerType) _1);
					:}
	|				T_LBRACKET ConstExpr:_1 T_RBRACKET
					{: 
						RESULT = ((MyParser) parser).DoArrayCheck((STO)_1);
					:}
    |               /* empty */
    ;
	
OptPointerList ::= 	PointerList:_1 {: RESULT = _1; :}
    |               /* empty */
    ;
    

PointerList ::=   	Pointer:_1 {: RESULT = _1; :}
    |               PointerList:_1 Pointer:_2
    				{:
    					RESULT = new PointerType ("*", 4, (PointerType)_1);
    				:}
    ;


Pointer ::=  	T_STAR
				{: 
					RESULT = new PointerType ("*", 4); 
				:}
    ;


ReturnType ::=      SubType:_1 OptPointerList:_2
                    {:
                        // You should determine the type and return it here
                        if(_2 != null) 
                        {
                            ((PointerType)_2).setType(_1);
                            RESULT = _2;
                        } 
                        else 
                        	RESULT = _1;
                    :}
    |               T_VOID {: RESULT = new VoidType("void", 0); :}
    ;


CodeBlock ::=       T_LBRACE
                    	{: ((MyParser) parser).DoBlockOpen(); :}
                    OptStmtList
		    			{: ((MyParser) parser).DoBlockClose(); :}
                    T_RBRACE
    ;


OptStmtList ::=     StmtList
    |               /* empty */
    ;


StmtList ::=        Stmt
    |               StmtList Stmt
    ;


Stmt ::=        VarDecl
    |               ConstDecl
    |               TypedefDecl
    |               CodeBlock
    |               Expr T_SEMI
    |               IfStmt
    |               WhileStmt
    |               BreakStmt
    |               ContinueStmt
    |               ExitStmt
    |               ReturnStmt
    |               ReadStmt
    |               WriteStmt
    |				NewStmt
    |				DeleteStmt
    ;


OptParamList ::=    ParamList:_1 {: RESULT = _1; :}
    |               /* empty */  {: RESULT = new Vector <VarSTO>(); :}
    ;


ParamList ::=       ParamDecl:_1
					{:
                        RESULT = new Vector <VarSTO>();
                        RESULT.addElement (_1);
                    :}
    |               ParamList:_1 T_COMMA ParamDecl:_3
    				{:
                        RESULT = _1;
                        RESULT.addElement (_3);
                    :}
    ;


ParamDecl ::=       Type:_1 OptRef:_2 T_ID:_3
					{:
                        RESULT = new VarSTO(_3, _1, (Boolean)_2);
                    :}
    ;


OptRef ::=          T_AMPERSAND 
					{: RESULT = true; :}
    |               /* empty */
    				{: RESULT = false; :}
    ;


IdList ::=          T_ID:_1
                    {:
                    	RESULT = new Vector();
                    	RESULT.addElement(new VarSTO(_1));
                    :}
    |               IdList:_1 T_COMMA OptModifiers T_ID:_3
                    {:
                        RESULT = _1;
                        RESULT.addElement(new VarSTO(_3));
                    :}
    ;


IdListUpper ::=     T_ID_U:_1
                    {:
                        RESULT = new Vector ();
                        RESULT.addElement (_1);
                    :}
    |               IdListUpper:_1 T_COMMA T_ID_U:_3
                    {:
                        RESULT = _1;
                        RESULT.addElement (_3);
                    :}
    ;

IdListWOptInit ::=  OptModifiers:_3 T_ID:_1 OptInit:_2
                    {:
                    	STO check = ((MyParser) parser).DoInitCheck(_1, (STO) _2);
                    	RESULT = new Vector ();
                    	if(_2 instanceof ErrorSTO || _2 instanceof ErrorSTO || check.isError() )
                    	{
                    		VariableBox<STO,STO> vb = new VariableBox<STO, STO>
                    		(new ErrorSTO(_1), null);
                            RESULT.addElement (vb);
                    	}
                    	else if(_2 == null)
                    	{
                    		if(_3 == null)
                    		{
                    			VarSTO var = new VarSTO(_1, null);
                    			RESULT.addElement( new VariableBox(var, null) );
                    		}
                    		else if (_3 != null)
                    		{
                    			VarSTO var = new VarSTO(_1, ((STO) _3).getType());
                    			RESULT.addElement( new VariableBox(var, null) );
                    		}
                    	}
                        else if(_2 != null)
                        {
                        	if(_3 == null)
                    		{
                    			VarSTO var = new VarSTO(_1, null);
                    			RESULT.addElement( new VariableBox(var, check) );
                    		}
                    		else if (_3 != null && !(_3 instanceof ErrorSTO))
                    		{
                    			VarSTO var = new VarSTO(_1, ((STO) _3).getType());
                    			RESULT.addElement( new VariableBox(var, check) );
                    		}
                        }
                    :}
    |               IdListWOptInit:_4 T_COMMA OptModifiers:_3 T_ID:_1 OptInit:_2
                    {:
                        STO check = ((MyParser) parser).DoInitCheck(_1, (STO) _2);
                    	RESULT = _4;
                    	if(_2 instanceof ErrorSTO || _2 instanceof ErrorSTO || check.isError() )
                    	{
                    		VariableBox<STO,STO> vb = new VariableBox<STO, STO>
                    		(new ErrorSTO(_1), null);
                            RESULT.addElement (vb);
                    	}
                    	else if(_2 == null)
                    	{
                    		if(_3 == null)
                    		{
                    			VarSTO var = new VarSTO(_1, null);
                    			RESULT.addElement( new VariableBox(var, null) );
                    		}
                    		else if (_3 != null)
                    		{
                    			VarSTO var = new VarSTO(_1, ((STO) _3).getType());
                    			RESULT.addElement( new VariableBox(var, null) );
                    		}
                    	}
                        else if(_2 != null)
                        {
                        	if(_3 == null)
                    		{
                    			VarSTO var = new VarSTO(_1, null);
                    			RESULT.addElement( new VariableBox(var, check) );
                    		}
                    		else if (_3 != null && !(_3 instanceof ErrorSTO))
                    		{
                    			VarSTO var = new VarSTO(_1, ((STO) _3).getType());
                    			RESULT.addElement( new VariableBox(var, check) );
                    		}
                        }
                    :}
    ;

IdListWInit ::=     OptModifiers T_ID:_1 T_ASSIGN ConstExpr:_2
                    {:
                    	STO s = ((MyParser) parser).DoInitCheck(_1, (STO)_2); //TODO: make sure this is needed
                    	STO sto = ((MyParser) parser).DoConstInit(_1, (STO)_2);
						RESULT = new Vector ();
						RESULT.addElement (sto);
                    :}
  //|       Do not need an extra rule since we are not doing constant arrays
    |               IdListWInit:_1 T_COMMA OptModifiers T_ID:_3 T_ASSIGN ConstExpr:_2
                    {:
                    	STO s = ((MyParser) parser).DoInitCheck(_3, (STO)_2); //TODO: make sure this is needed
                    	STO sto = ((MyParser) parser).DoConstInit(_3, (STO)_2);
						RESULT = _1;
						RESULT.addElement (sto);
		    :}
    ;


OptInit ::=         T_ASSIGN Expr:_1 {: RESULT = _1; :}
    |               T_ASSIGN T_LBRACE ArrElemsList T_RBRACE
    |               /* empty */
    ;

ArrElemsList ::=    Expr
    |               ArrElemsList T_COMMA Expr
    ;

IfStmt ::=          T_IF 
					Expr:_1 
					{: 
						RESULT = ((MyParser) parser).DoIfWhileCheck (_1, true); 
					:} 
					CodeBlock 
					OptElse
    ;


OptElse ::=         T_ELSE CodeBlock
    |               /* empty */
    ;


WhileStmt ::=       T_WHILE 
					{:
						((MyParser) parser).InWhile();
					:}
					Expr:_1 
					{: 
						RESULT = ((MyParser) parser).DoIfWhileCheck (_1, false); 
					:}  
					CodeBlock
					{:
						((MyParser) parser).OutWhile();
					:}
    ;


BreakStmt ::=       T_BREAK T_SEMI
					{:
						((MyParser) parser).DoBreakCheck();
					:}
    ;


ContinueStmt ::=    T_CONTINUE T_SEMI
					{:
						((MyParser) parser).DoContinueCheck();
					:}
    ;


ExitStmt ::=        T_EXIT T_LPAREN Expr:_1 T_RPAREN T_SEMI
					{:
                        RESULT = ((MyParser) parser).DoExitStmtCheck(_1);
                    :}
    ;


ReturnStmt ::=   T_RETURN T_SEMI
					 {: ((MyParser) parser).DoReturnStmtCheck(null); :}
    |               T_RETURN Expr:_1 T_SEMI
    				 {: ((MyParser) parser).DoReturnStmtCheck(_1); :}
    ;


ReadStmt ::=        T_CIN T_ISTREAM Designator T_SEMI
    ;


WriteStmt ::=       T_COUT T_OSTREAM WritePairList T_SEMI
    ;


WritePairList ::=   WritePair
    |               WritePairList T_OSTREAM WritePair
    ;


WritePair ::=    Expr
    |               T_ENDL
    ;


ConstExpr ::=    Expr:_1 {: RESULT = _1; :}
    ;


OptExprList ::=     ExprList:_1 {: RESULT = _1; :}
    |               {: RESULT = new Vector (); :}
    ;


ExprList ::=     	Expr:_1
					{:
                        RESULT = new Vector ();
                        ((Vector) RESULT).addElement (_1);
                    :}
    |               ExprList:_1 T_COMMA Expr:_2
    				{:
                        ((Vector) _1).add(_2);
                        RESULT = _1;
                    :}
    ;

Expr ::=         Designator:_1 T_ASSIGN Expr:_2
                    {:
                        RESULT = ((MyParser) parser).DoAssignExpr (_1, _2);
                    :}
    |               Expr0:_1
                    {:
						RESULT = _1;
                    :}
    ;


Expr0 ::=        Expr0:_1 T_OR:_2 Expr1:_3
                    {:
						Operator op = new BooleanOp("||");
                        if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Expr1:_1
                    {:
						RESULT = _1;
                    :}
    ;


Expr1 ::=        Expr1:_1 T_AND:_2 Expr2:_3
                    {:
						Operator op = new BooleanOp("&&");
                        if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Expr2:_1
                    {:
						RESULT = _1;
                    :}
    ;


Expr2 ::=        Expr2:_1 T_BAR:_2 Expr3:_3
                    {:
						Operator op = new BitwiseOp("|");
                        if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Expr3:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr3 ::=        Expr3:_1 T_CARET:_2 Expr4:_3
                    {:
						Operator op = new BitwiseOp("^");
                        if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Expr4:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr4 ::=        Expr4:_1 T_AMPERSAND:_2 Expr5:_3
                    {:
						Operator op = new BitwiseOp("&");
                        if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Expr5:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr5 ::=           Expr5:_1 Equality:_2 Expr6:_3
                    {:
                        Operator op = new ComparisonOp(_2);
                        if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Expr6:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr6 ::=           Expr6:_1 Relation:_2 Expr7:_3
                    {:
                    	Operator op = new ComparisonOp(_2);
                        if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Expr7:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr7 ::=           Expr7:_1 AddOp:_2 Expr8:_3
                    {:
                    	Operator op = new ArithmeticOp(_2);
                    	if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Expr8:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr8 ::=        Expr8:_1 MulOp:_2 Designator:_3
                    {:
                        Operator op = new ArithmeticOp(_2);
                    	if ( (_1 instanceof ErrorSTO) || (_3 instanceof ErrorSTO) )
                        	RESULT = new ErrorSTO ("error");
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr (_1, op, _3);
                    :}
    |               Designator:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Equality ::=     	T_EQU
					{:
						RESULT = "==";
					:}
    |               T_NEQ
    				{:
						RESULT = "!=";
					:}
    ;


Relation ::=     	T_LT
					{:
						RESULT = "<";
					:}
    |               T_GT
    				{:
						RESULT = ">";
					:}
    |               T_LTE
    				{:
						RESULT = "<=";
					:}
    |               T_GTE
    				{:
						RESULT = ">=";
					:}
    ;
    

AddOp ::=       T_PLUS
                    {:
                        RESULT = "+";
                    :}
    |               T_MINUS
                    {:
                        RESULT = "-";
                    :}
    ;


MulOp ::=       T_STAR
                    {:
                        RESULT = "*";
                    :}
    |               T_SLASH
                    {:
                        RESULT = "/";
                    :}
    |               T_MOD
                    {:
                        RESULT = "%";
                    :}
    ;


IncDecOp ::=        T_PLUSPLUS
                    {:
                        RESULT = "++";
                    :}
    |               T_MINUSMINUS
                    {:
                        RESULT = "--";
                    :}
    ;


Designator ::= T_STAR Designator:_1
                    {:
						RESULT = ((MyParser) parser).DoDereferenceCheck(_1);
                    :}
    |               T_AMPERSAND Designator:_1
                    {:
						RESULT = ((MyParser) parser).DoAddressOf(_1);
                    :}
    |               UnarySign:_2 Designator:_1
                    {:
						RESULT = ((MyParser) parser).DoUnarySign((String)_2, _1);
                    :}
    |               T_NOT Designator:_1
                    {:
            			Operator op = new UnaryOp("!");
						RESULT = ((MyParser) parser).DoUnaryOp(op, _1); 
                    :}
    |               T_SIZEOF T_LPAREN Designator:_2 T_RPAREN
                    {:
						RESULT = ((MyParser) parser).DoSizeof(_2);
                    :}
    |               T_SIZEOF T_LPAREN Type:_2 T_RPAREN
                    {:
						RESULT = ((MyParser) parser).DoSizeof(_2);
                    :}
    |               T_LPAREN Type:_1 T_RPAREN Designator:_2
                    {:
						RESULT = ((MyParser) parser).DoTypeCast(_1, _2);
                    :}
    |               IncDecOp:_1 Designator:_2
                    {:
                    	Operator op = new UnaryOp(_1, true);
						RESULT = ((MyParser) parser).DoUnaryOp(op, _2);
                    :}
    |               Designator2:_1 
                    {:
                        RESULT = _1;
                    :}
    ;


Designator2 ::=     Designator2:_1 T_DOT T_ID:_3
                    {:
                        RESULT = ((MyParser) parser).DoDesignator2_Dot (_1, _3);
                    :}
    |               Designator2:_1 T_LBRACKET Expr:_2 T_RBRACKET
                    {:
                    	if(_2 instanceof ErrorSTO)
                    	{
                    		RESULT = new ErrorSTO ("Error");
                    	}
                    	else
                    	{
                        	RESULT = ((MyParser) parser).DoDesignator2_Array (_1, _2);
                        }
                    :}
    |               Designator2:_1 T_ARROW T_ID:_2
                    {:
                        RESULT = ((MyParser) parser).DoArrowCheck(_1, _2);
                    :}
    |               Designator2:_1 IncDecOp:_2
                    {:
                        Operator op = new UnaryOp(_2, false);
						RESULT = ((MyParser) parser).DoUnaryOp(op, _1);
                    :}
    |               Designator2:_1 T_LPAREN OptExprList:_2 T_RPAREN
                    {:
                        RESULT = ((MyParser) parser).DoFuncCall (_1, (Vector)_2);;
                    :}
    |               Designator3:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Designator3 ::=     T_LPAREN Expr:_2 T_RPAREN
                    {:
                        RESULT = _2;
                    :}
    |               T_INT_LITERAL:_1
                    {:
                        RESULT = new ConstSTO (_1, new IntType("int", 4));
                    :}
    |               T_FLOAT_LITERAL:_1
                    {:
                        RESULT = new ConstSTO (_1, new FloatType("float", 4));
                    :}
    |               T_CHAR_LITERAL:_1
                    {:
                        RESULT = new ConstSTO (_1);
                    :}
    |               T_STR_LITERAL:_1
                    {:
                        RESULT = new ConstSTO (_1);
                    :}
    |               T_TRUE
                    {:
                        RESULT = new ConstSTO ("true", new BoolType("bool", 4));
                    :}
    |               T_FALSE
                    {:
                        RESULT = new ConstSTO ("false", new BoolType("bool", 4));
                    :}
    |               T_NULLPTR
                    {:
                        RESULT = new ConstSTO ("nullptr", new NullPointerType ("nullptr", 4));
                    :}
    |               T_COLONCOLON T_ID:_1
                    {:
                        RESULT = ((MyParser) parser).DoDesignator3a_ID (_1);
                    :}
    |               T_ID:_1
                    {:
                        RESULT = ((MyParser) parser).DoDesignator3_ID (_1);
                    :}
    ;


QualIdent ::=       T_ID_U:_1
                    {:
                       RESULT = ((MyParser) parser).DoQualIdent (_1);
                    :}
    ;
    
NewStmt ::=		T_NEW Designator:_1 T_SEMI
				{:
					((MyParser) parser).DoNewStmtCheck(_1);
				:}
	;

DeleteStmt ::=	T_DELETE Designator:_1 T_SEMI
				{:
					((MyParser) parser).DoDeleteStmtCheck(_1);
				:}
	;
	
UnarySign ::=  	T_MINUS
				{:
					RESULT = "-";
				:}
	|			T_PLUS
				{:
					RESULT = "+";
				:}
	;
